// License Information:
// Scyndi's Forest Quest
// New User
// 
// 
// 
// (c) Jeroen P. Broks, 2012, 2015, 2023
// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
// 
// Please note that some references to data like pictures or audio, do not automatically
// fall under this licenses. Mostly this is noted in the respective files.
// 
// Version: 23.01.19
// End License Information

Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/Graphics"

#use "Script/Link/Back"
#use "Script/Link/ScyndiSays"

#use "Script/Link/User"

#use "Script/Use/Mouse"

Const Var PicNext = ObtainImage("Next")
const Var Fnt = ObtainFont("Base")
const int NH = 50

int stage

global void ComeToMe()
	stage = 1
	GoToFlow("NEWUSER")
End

global void Scyndi_Back()
	Back.Background()
end

const table first = NewArray('ABCDEFGHIJKLMNOPQRSTUVWXYZ')
const table other = NewArray('abcdefghijklmnopqrstuvwxyz_0123456789')
const table shw = { [true]=first, [false]=other }

string _Name = ""

void EnterUserName()
	//static plua _Name = ""
	static plua LW = Graphics.Width div 10
	static plua CW = Graphics.Width div 20
	static plua LH = 50
	_Name = _Name || ""
	if Sys.BuildState=="debug"
		fnt.Text(sprintf("Name %02d: %s",len(_name),_name),0,0)
	end
	SetColorHSV(os.time() % 360,1,1)
	fnt.text(_Name,5,5)
	if len(_Name)<15
		for i,v in ipairs(shw[_Name==""])
			int LX
			int LY
			LX = ((i%10)*LW)+CW
			LY = 100+(LH*(i div 10))
			// printf("#%d chr: %d/%s  (%d,%d) %dx%d\n",i,v,Chr(v),LX,LY,Graphics.Width,Graphics.Height)
			if Events.MouseY>LY && Events.MouseX>LX-10 && Events.MouseX<LX+10 && Events.MouseY<LY+LH
				SetColor(255,180,0)
				if Events.MouseHit()
					_Name = _Name .. Chr(v)
					print(_Name,v,Chr(v))
					Events.Flush()
				End
			Else
				SetColor(255,255,255)
			End
			Fnt.Text(Chr(v),LX,LY)
		end
	end
	if _Name!=""
		int nx = Graphics.Width-PicNext.Width
		int ny = Graphics.Height-PicNext.Height
		if Events.MouseHit(3)
			_Name = Left(_Name,len(_Name)-1)
		elif Events.MouseX>nx && Events.MouseY>ny
			setColor(255,255,255,255)
			if Events.MouseHit()
				stage++
				Events.Flush()
				User.User = _Name
				_Name=""
			end
		else
			setColor(127,127,127,127)
		end
		PicNext.Draw(nx,ny)	
		setcolor(255,255,255,255)
	end
End

global void MainFlow()
	Scyndi_Back()
	if Events.AppTerminate() 
		Sys.Exit()
	end
	switch stage
		case 0
			stage++
			fallthrough
		case 1
			//Print("Scyndi welcomes you")
			ScyndiSays.Say("Scyndi.Intro.A")
			stage++
		case 2
			ScyndiSays.Say("Scyndi.Intro.B",true)
			stage++
		case 3
			EnterUserName()
		case 4
			ScyndiSays.Say("Scyndi.Intro.C")
			stage++
		case 5
			ScyndiSays.Say("Scyndi.Intro.D",true)
			stage++
			
		default
			error(sprintf("I do not know how to handle New User stage #%d",stage))
	end
	Mouse.Show()
end